top_level ::= database_declaration
case_sensitive = false

identifier ::= standard ada_identifier
integer    ::= standard ada_numeric_literal
string     ::= standard ada_string_literal
line_comment       = "--"

format list_of_declarations indented_child
format list_of_fields indented_child

format ';' no_space_before no_space_after new_line_after
format '.' no_space_before no_space_after
format ',' no_space_before space_after
format '(' space_before no_space_after
format ')' no_space_before space_after

database_declaration ::=
    { context_clause }
    'package' name 'is'
        list_of_declarations
    'end' [ 'database' ] name ';'

context_clause ::= 'with' identifier ';'

list_of_declarations ::= { declaration ';' }

declaration ::= record_declaration
              | type_declaration

record_declaration ::= 'record' identifier [ ':' base_records ] [ 'with' list_of_attributes ]
                         [ 'is' list_of_fields 'end' [ 'record' ] matching_identifier ]

base_records ::= < identifier / ',' >

matching_identifier ::= identifier

list_of_attributes ::= < identifier / ',' >

list_of_fields ::= { field ';' }

field ::= data_field | compound_key_field

data_field ::= [ key_spec ] identifier [ ':' named_type ] [ ':=' static_expression ] [ 'is' list_of_attributes ]

static_expression ::= integer | string

compound_key_field ::= key_spec identifier 'with' compound_key_fields

compound_key_fields ::= < identifier / ',' >

key_spec ::= [ 'unique' ] 'key'

named_type ::= identifier [ constraint ]

constraint ::= '(' integer ')'

enumeration_type ::= '(' < identifier / ',' > ')'

type_declaration ::= 'type' identifier 'is' type_spec

type_spec ::= named_type | enumeration_type

name ::= < identifier / '.' >
