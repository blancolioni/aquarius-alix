top_level ::= program
case_sensitive = false

identifier         ::= standard ada_identifier
numeric_literal    ::= standard ada_numeric_literal
character_literal  ::= standard ada_character_literal
string_literal     ::= standard backslash_escaped_string
delimiter          ::= delimiters "&()+,;|@{}"

line_comment        = "--"
block_comment_start = "(*"
block_comment_end   = "*)"

format ';' no_space_before new_line_after
format '{' space_before new_line_after indent_after
format '}' outdent_before soft_new_line_after
format '(' space_before no_space_after
format ')' no_space_before space_after
format feature_declaration indented_child
format let_binding indented_child

program ::= < class_declaration ';' >

class_declaration ::= 'class' class_header '{' { feature_declaration ';' } '}' 

class_header ::= defining_class_name  [ inherited_class_name ]

defining_class_name ::= identifier

inherited_class_name ::= [ 'inherits' type_name ]

feature_declaration ::= method_feature | property_feature

method_feature ::= identifier '(' { formal / ',' } ')' ':' type_name '{' expr '}'

property_feature ::= identifier ':' type_name [ '<-' expr ]
		  
formal ::= identifier ':' type_name

expr ::= assignment_expr 
	   | while_expr
	   | compound_expr
	   | let_expr
	   | case_expr
	   | isvoid_expr
	   | boolean_expr
	   
assignment_expr ::= identifier '<-' expr
if_expr ::= 'if' expr 'then' expr 'else' expr 'fi'
while_expr ::= 'while' expr 'loop' expr 'pool'
compound_expr ::= '{' < expr ';' > '}'
let_expr ::= 'let' < let_binding  / ',' > 'in' expr
case_expr ::= 'case' expr 'of' < identifier ':' type_name '=>' expr ';' > 'esac'
isvoid_expr ::= 'isvoid' expr

let_binding ::= identifier ':'  type_name  [ '<-' expr ]

boolean_expr ::= [ 'not' ] relation_expr

relation_expr ::= term { relation_op term }

term ::= factor { addition_op factor }

factor ::= void_check { multiplication_op void_check }

void_check ::= [ 'isvoid' ] logical_expr

logical_expr ::= [ '~' ] method_selector

method_selector ::= primary { [ '@' type_name ] '.' primary }

relation_op ::= '<=' | '<' | '='

addition_op ::= '+' | '-'

multiplication_op ::= '*' | '/'

primary ::= identifier [ '(' { expr / ',' } ')' ]
     	  | 'new' type_name
    	  | '(' expr ')'
          | if_expr
	      | numeric_literal
	      | string_literal
	      | 'true'
	      | 'false'

type_name ::= identifier