before Compilation_Unit do
--   IO.Put_Line ("Ada: defining subprogram names");
end;

after Compilation_Unit do
--   Aqua.Report_State;
end;

after procedure_body/procedure_specification do
   Komnenos.Define (parent, child.name, "procedure-declaration");
end;

after function_body/function_specification do
   Komnenos.Define (parent, child.name, "function-declaration");
end;

after procedure_specification/defining_qualified_reference 
   or function_specification/defining_qualified_reference
do
   parent.name := child.name;
end;

after function_specification/string_literal do
   parent.name := child.text;
end;

after package_spec/defining_qualified_reference do
   Komnenos.Define (parent, defining_qualified_reference.image, "package-declaration");
end;

before defining_qualified_reference do
   tree.Qualifier := null;
   tree.Last_Identifier := null;
   tree.Last_Child := null;
end;

after defining_qualified_reference/identifier do
   if parent.last_child then
      parent.Last_Child.last := 0;
   end if;
   
   child.last := 1;
   parent.Last_Child := child;
   parent.Name := identifier.Image;
   if parent.Last_Identifier then
      if parent.Qualifier then
         parent.Qualifier :=
           parent.Qualifier & "." & parent.Last_Identifier.Image;
      else
         parent.Qualifier := parent.Last_Identifier.Image;
      end if;      
      identifier.full_name := parent.qualifier & "." & identifier.text;
   else
      identifier.full_name := identifier.text;
   end if;
   parent.Last_Identifier := identifier;

end;

