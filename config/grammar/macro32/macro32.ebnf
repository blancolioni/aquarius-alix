top_level ::= source_file

end_of_line ::= !\n!
identifier  ::= ![\l_][\w$]*!
integer     ::= !\d+!
string      ::= standard ada_string_literal
delimiter   ::= delimiters ".:$#()+-@=,!"
comment     ::= !;[.]*!

format '(' space_before no_space_after
format ')' no_space_before space_after
format '+' no_space_before space_after
format '-' space_before no_space_after
format ',' no_space_before space_after
format '#' space_before no_space_after
format '@' space_before no_space_after
format '=' space_before space_after
format ':' no_space_before new_line_after
format '$' space_before no_space_after
format '.' space_before no_space_after

format integer no_space_after
format source_element new_line_after

source_file ::= { source_element end_of_line }

source_element ::= [ label ':' ] [ instruction ]

label ::= identifier | integer

instruction ::= pseudo_op | op

pseudo_op ::= directive | declaration

directive ::= '.' identifier { directive_operand }

directive_operand ::= integer | identifier | string

op ::= no_operand
     | single_operand
     | double_operand
     | triple_operand
     | branch
     | jump
     | call
     | goto
     | trap
     | return
     
no_operand ::= 'halt' | 'nop' | 'rts'
single_operand ::= single_operand_instruction [size] arg
double_operand ::= double_operand_instruction [size] arg ',' arg
triple_operand ::= double_operand_instruction [size] arg ',' arg ',' arg

branch ::= branch_instruction branch_destination
branch_destination ::= identifier | temporary_label

jump ::= jump_instruction jump_destination
jump_destination ::= identifier | temporary_label

call ::= 'call' integer ',' arg
goto ::= 'goto' arg
return ::= 'return' integer

temporary_label ::= label_direction integer
label_direction ::= '+' | '-'

trap ::= 'trap' trap_operand

trap_operand ::= identifier | integer

size ::= '.' integer

single_operand_instruction ::=
                  'clr' | 'not'
                 | 'inc' | 'dec' | 'neg'
                 | 'adc' | 'sbc' | 'tst'
                 | 'ldj' | 'stj'
                 
double_operand_instruction ::=
     'mov' | 'cmp' | 'add' | 'sub' | 'mul' | 'div' | 'mod' | 'and' | 'or' | 'xor'
   | 'seq' | 'sne' | 'sgt' | 'slt' | 'sge' | 'sle'
  
branch_instruction ::= 
          'br' | 'bne' | 'beq' | 'bge' | 'blt' | 'bgt' | 'ble'
         | 'bpl' | 'bmi' | 'bhi' | 'blos' 
         | 'bvc' | 'bvs' | 'bcc' | 'bcs'
         | 'bhis' | 'blo'

jump_instruction ::= 'jmp' | 'jsr'

arg ::= identifier | deferred
      | autoincrement
      | autodecrement
      | indexed | indexed_deferred
      | immediate
      | subroutine
      
deferred ::= '(' identifier ')' | '@' identifier
autoincrement ::= '(' identifier ')' '+'
autodecrement ::= '-' '(' identifier ')'
indexed ::= operand '(' identifier ')'
indexed_deferred ::= '@' operand '(' identifier ')'
subroutine ::= '{' identifier '}'
immediate ::= '#' expression

expression ::= < operand / operator >

operand ::= identifier | integer | negative_integer

negative_integer ::= '-' integer

operator ::= '+' | '-'

declaration ::= identifier '=' expression
